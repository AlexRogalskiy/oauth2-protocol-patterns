buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-tomcat-plugin:2.5'
        classpath 'com.bmuschko:gradle-cargo-plugin:2.3'
    }
}

apply plugin: 'com.bmuschko.tomcat'
apply plugin: 'com.bmuschko.cargo'

repositories {
    mavenCentral()
}

ext.tomcatHomeDir = System.getenv("TOMCAT_HOME")
ext.tomcatEmbedVersion = '8.0.42'
ext.uaaVersion = '4.20.0'

configurations {
    uaa
}

dependencies {
    tomcat "org.apache.tomcat.embed:tomcat-embed-core:${tomcatEmbedVersion}",
            "org.apache.tomcat.embed:tomcat-embed-logging-juli:${tomcatEmbedVersion}",
            "org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatEmbedVersion}"

    uaa("org.cloudfoundry.identity:cloudfoundry-identity-uaa:${uaaVersion}@war") {
        artifact {
            name = "cloudfoundry-identity-uaa"
            extension = 'war'
            type = 'war'
        }
    }
}

tomcatRunWar {
    webApp = file("cloudfoundry-identity-uaa-${uaaVersion}.war")
    contextPath = "uaa"
    httpPort = 8090
}

task downloadUAA(type: Copy) {
    from configurations.uaa
    into "$projectDir"
}

cargo {
    containerId = 'tomcat8x'
    port = 8090

    deployable {
        file = file("cloudfoundry-identity-uaa-${uaaVersion}.war")
        context = 'uaa'
    }

    local {
        homeDir = file("$tomcatHomeDir")
        outputFile = file('uaa-server.log')
        systemProperties {
            property "UAA_CONFIG_PATH", "$projectDir"
        }
    }
}
